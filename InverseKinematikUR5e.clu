// dh parameters UR5e
a2 = -0.425;
a3 = -0.3922;
d1 = 0.1625;
d4 = 0.1333;
d5 = 0.0997;
d6 = 0.0996;

Pi = 3.14159265;

// position of the end-effector and joint 5
P_e = createPoint(p_x, p_y, p_z);
P_5 = createPoint(p_x - d6 * 0, p_y - d6 * 0, p_z - d6 * 1);

// sphere around P_5
S_c = P_5 - (1/ 2 * d4 * d4 * einf);
// sphere around the origin
K_0 = e0 + (S_c . e0) * einf;
// intersection of S_c and K_0;
C_5k = S_c ^ K_0;
// intersection of C_5k and the horizontal plane through P_5
Q_c = Dual(C_5k . (P_5 ^ e1 ^ e2 ^ einf));
// point P_c with an offset d4 from P_5
P_c = ExtractFirstPoint(Q_c);
// plane through joints 1, 2, 3 and 4
PI_c = Dual(e0 ^ -e3 ^ P_c ^ einf);


// finding P_3 and P_4
// plane parallel to PI_c that contains P_4 and P_5
PI_c_parallel = PI_c + (P_5 . PI_c) * einf; // eq. 47
PI_56_orthogonal = Dual(Dual(P_5 ^ P_e) ^ einf); // eq. 48 l.1

n_56_orthogonal = -((Dual(PI_56_orthogonal) . e0)  . einf) / abs((Dual(PI_56_orthogonal) . e0) . einf); // eq. 48, l. 2
PI_c_orthogonal = Dual(P_5 ^ n_56_orthogonal ^ einf);
L_45 = PI_c_parallel ^ PI_c_orthogonal;

S_5 = P_5 - (1 / 2 * d5 * d5 * einf);
Q_4 = Dual(L_45 . Dual(S_5));
P_4 = ExtractFirstPoint(Q_4);

// point P3
S_4 = P_4 + (1 / 2 * d4 * d4 * einf);
L_34 = Dual(P_4 ^ PI_c ^ einf);
Q_3 = Dual(Dual(S_4) . (L_34));
P_3 = ExtractFirstPoint(Q_3);

// finding P1 and P2
P_1 = createPoint(0, 0, -d1);
S_1 = P_1 - (1 / 2 * a2 * a2 * einf);
S_3 = P_3 + (1 / 2 * a3 * a3 * einf);
C_2 = S_1 ^ S_3;
Q_2 = Dual(Dual(C_2) . PI_c);
P_2 = ExtractFirstPoint(Q_2);

// finding the joint angles
L_01 = Dual(e0 ^ -e3 ^ einf);
L_12 = Dual(P_1 ^ P_2 ^ einf);
L_23 = Dual(P_2 ^ P_3 ^ einf);

P_0 = createPoint(0,0,0);

a_1 = e2;
b_1 = -(PI_c);
N_1 = e1 ^ e2;
?x1 = (a_1 ^ b_1) / N_1;
?y1 = a_1 . b_1;

a_2 = (Dual(L_01) . e0) . einf;
b_2 = (Dual(L_12) . e0) . einf;
N_2 = -(Dual(PI_c) . e0) . einf);
?x2 = (a_2 ^ b_2) / N_2;
?y2 = a_2 . b_2;

a_3 = (Dual(L_12) . e0) . einf;
b_3 = (Dual(L_23) . e0) . einf;
N_3 = -(Dual(PI_c) . e0) . einf);
?x3 = (a_3 ^ b_3) / N_1;
?y3 = a_3 . b_3;
	
a_4 = (Dual(L_23) . e0) . einf;
b_4 = (Dual(L_45) . e0) . einf;
N_4 = -(Dual(PI_c) . e0) . einf);
?x4 = (a_4 ^ b_4) / N_4;
?y4 = a_4 . b_4;
	
a_5 = P_c;
b_5 = -VecN3(0, 0, 1);
N_5 = (-L_45 ^ e0) . einf;
?x5 = (a_5 ^ b_5) / N_5;
?y5 = a_5 . b_5;

a_6 = (Dual(L_45) . e0) . einf;
b_6 = -e2;
N_6 = -e3 * (e3 ^ e2 ^ e1);
?x6 = (a_6 ^ b_6) / N_6;
?y6 = a_6 . b_6;
